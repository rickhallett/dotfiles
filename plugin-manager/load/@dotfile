#!/usr/bin/zsh
#
# This loads a folder from within the dotfiles repo.
# That involves the following:
# - bin/           - anything in bin/ will get added to your path and be made
#                    available anywhere.
# - *.zsh          - any file ending in .zsh will get loaded into your
#                    environment.
# - path.zsh       - any file named path.zsh is loaded first and is expected to
#                    setup $PATH or similar.
# - completion.zsh - any file named completion.zsh is loaded last and is
#                    expected to setup autocomplete.
# - install.sh     - any file named install.sh is executed when you run
#                    script/install, and is not loaded in the shell.
# - *.symlink      - any file ending in .symlink will get symlinked to the home
#                    folder (with the suffix removed) when you run
#                    script/bootstrap, and is used to set up configuration etc.
#
# The commands to execute will be echoed to stdout.

set -euo pipefail

readonly DOTFILE_FOLDER="${0:A:h:h:h}"
readonly FOLDER="${DOTFILE_FOLDER}/${1?provide the folder to use}"

if [ ! -d "${FOLDER}" ]; then
    echo "the folder ${FOLDER} does not exist" >&2
fi

if [ -d "${FOLDER}/bin" ]; then
    echo 'export PATH="${PATH}:'"${FOLDER}/bin"'"'
fi

if [ -e "${FOLDER}/path.zsh" ]; then
    echo "source '${FOLDER}/path.zsh'"
fi

for file in ${FOLDER}/*.zsh(N); do
    if [ "${file:t}" = "path.zsh" ]; then
        continue
    fi
    if [ "${file:t}" = "completion.zsh" ]; then
        continue
    fi
    echo "source '${file}'"
done

if [ -e "${FOLDER}/completion.zsh" ]; then
    echo "source '${FOLDER}/completion.zsh'"
fi
